FUNCTION_BLOCK FuzzyContextAware

VAR_INPUT
        bufferNeighbor      : REAL;
        energyNeighbor      : REAL;
        popularityNeighbor  : REAL;
        tieStrengthNeighbor : REAL;

        ABILITY             : REAL;
        SOCIAL              : REAL;
END_VAR

VAR_OUTPUT
        ABILITY_NODE            : REAL;
        SOCIAL_IMPORTANCE       : REAL;

        TRANSFER_OPPORTUNITY    : REAL;
END_VAR

FUZZIFY bufferNeighbor
         TERM low  := TRAPE  0.0 0.0  0.25 0.4  ;
         TERM med  := TRAPE  0.3 0.45 0.55 0.7 ;
         TERM high := TRAPE  0.6 0.75 1.0  1.0 ;
END_FUZZIFY

FUZZIFY energyNeighbor
         TERM low    := TRAPE 0.0 0.0  0.25 0.4 ;
         TERM med    := TRAPE 0.3 0.45 0.55 0.7 ;
         TERM high   := TRAPE 0.6 0.75 1.0  1.0 ;
END_FUZZIFY

DEFUZZIFY ABILITY_NODE
        TERM vbad       := TRAPE 0.0 0.0 0.2 0.3 ;
        TERM bad        := TRAPE 0.2 0.3 0.4 0.5 ;
        TERM good       := TRAPE 0.4 0.6 0.7 0.8 ;
        TERM perfect    := TRAPE 0.7 0.8 1.0 1.0 ;
        METHOD  : COG;
        DEFAULT := 0;
        RANGE := (0.0 .. 1.0);
END_DEFUZZIFY

RULEBLOCK ABILITY
        AND     : MIN;
        ACT     : MIN;
        ACCU    : MAX;

        RULE 1 : IF bufferNeighbor IS high AND energyNeighbor IS high THEN ABILITY_NODE IS perfect;
        RULE 2 : IF bufferNeighbor IS high AND energyNeighbor IS med THEN ABILITY_NODE IS perfect;
        RULE 3 : IF bufferNeighbor IS high AND energyNeighbor IS low THEN ABILITY_NODE IS bad;
        RULE 4 : IF bufferNeighbor IS med AND energyNeighbor IS high THEN ABILITY_NODE IS perfect;
        RULE 5 : IF bufferNeighbor IS med AND energyNeighbor IS med THEN ABILITY_NODE IS good;
        RULE 6 : IF bufferNeighbor IS med AND energyNeighbor IS low THEN ABILITY_NODE IS bad;
        RULE 7 : IF bufferNeighbor IS low AND energyNeighbor IS high THEN ABILITY_NODE IS good;
        RULE 8 : IF bufferNeighbor IS low AND energyNeighbor IS med THEN ABILITY_NODE IS bad;
        RULE 9 : IF bufferNeighbor IS low AND energyNeighbor IS low THEN ABILITY_NODE IS vbad;
END_RULEBLOCK

FUZZIFY popularityNeighbor
        TERM slow   := TRAPE  0.0 0.0  0.25 0.4 ;
        TERM med    := TRAPE  0.3 0.45 0.55 0.7 ;
        TERM fast   := TRAPE  0.6 0.75 1.0  1.0 ;
END_FUZZIFY

FUZZIFY tieStrengthNeighbor
        TERM poor   := TRAPE  0.0 0.0  0.25 0.4 ;
        TERM fair   := TRAPE  0.3 0.45 0.55 0.7 ;
        TERM good   := TRAPE  0.6 0.75 1.0  1.0 ;
END_FUZZIFY

DEFUZZIFY SOCIAL_IMPORTANCE
        TERM bad        := TRAPE 0.0 0.0  0.25 0.4 ;
        TERM good       := TRAPE 0.3 0.45 0.55 0.7 ;
        TERM perfect    := TRAPE 0.6 0.75 1.0  1.0 ;
        METHOD  : COG;
        DEFAULT := 0;
        RANGE := (0.0 .. 1.0);
END_DEFUZZIFY

RULEBLOCK SOCIAL
        AND     : MIN;
        ACT     : MIN;
        ACCU    : MAX;

        RULE 1 : IF popularityNeighbor IS fast AND tieStrengthNeighbor IS good THEN SOCIAL_IMPORTANCE IS perfect;
        RULE 2 : IF popularityNeighbor IS fast AND tieStrengthNeighbor IS fair THEN SOCIAL_IMPORTANCE IS good;
        RULE 3 : IF popularityNeighbor IS fast AND tieStrengthNeighbor IS poor THEN SOCIAL_IMPORTANCE IS good;
        RULE 4 : IF popularityNeighbor IS med AND tieStrengthNeighbor IS good THEN SOCIAL_IMPORTANCE IS good;
        RULE 5 : IF popularityNeighbor IS med AND tieStrengthNeighbor IS fair THEN SOCIAL_IMPORTANCE IS good;
        RULE 6 : IF popularityNeighbor IS med AND tieStrengthNeighbor IS poor THEN SOCIAL_IMPORTANCE IS bad;
        RULE 7 : IF popularityNeighbor IS slow AND tieStrengthNeighbor IS good THEN SOCIAL_IMPORTANCE IS good;
        RULE 8 : IF popularityNeighbor IS slow AND tieStrengthNeighbor IS fair THEN SOCIAL_IMPORTANCE IS bad;
        RULE 9 : IF popularityNeighbor IS slow AND tieStrengthNeighbor IS poor THEN SOCIAL_IMPORTANCE IS bad;
END_RULEBLOCK


FUZZIFY ABILITY    // ABILITY_NODE input
    TERM vbad       := TRAPE 0.0 0.0 0.2 0.3 ;
    TERM bad        := TRAPE 0.2 0.3 0.4 0.5 ;
    TERM good       := TRAPE 0.4 0.6 0.7 0.8 ;
    TERM perfect    := TRAPE 0.7 0.8 1.0 1.0 ;
END_FUZZIFY

FUZZIFY SOCIAL   // socialImportance input
    TERM bad        := TRAPE 0.0 0.0 0.2 0.3;
    TERM good       := TRAPE 0.2 0.3 0.5 0.6;
    TERM perfect    := TRAPE 0.5 0.6 1.0 1.0;
END_FUZZIFY

DEFUZZIFY TRANSFER_OPPORTUNITY
     TERM low       := TRAPE 0.0 0.0 0.2 0.40 ;
     TERM med       := TRAPE 0.3 0.45 0.55 0.7 ;
     TERM high      := TRAPE 0.6 0.7 0.8 0.9 ;
     TERM vhigh     := TRAPE 0.8 0.9 1.0 1.0 ;
     METHOD : COG;
     DEFAULT := 0;
     RANGE := (0.0 .. 1.0);
END_DEFUZZIFY

RULEBLOCK TRANSFER
        AND     : MIN;
        ACT     : MIN;
        ACCU    : MAX;

        RULE 1 : IF ABILITY IS perfect AND SOCIAL IS perfect THEN TRANSFER_OPPORTUNITY IS vhigh;
        RULE 2 : IF ABILITY IS perfect AND SOCIAL IS good THEN TRANSFER_OPPORTUNITY IS vhigh;
        RULE 3 : IF ABILITY IS perfect AND SOCIAL IS bad THEN TRANSFER_OPPORTUNITY IS med;
        RULE 4 : IF ABILITY IS good AND SOCIAL IS perfect THEN TRANSFER_OPPORTUNITY IS high;
        RULE 5 : IF ABILITY IS good AND SOCIAL IS good THEN TRANSFER_OPPORTUNITY IS high;
        RULE 6 : IF ABILITY IS good AND SOCIAL IS bad THEN TRANSFER_OPPORTUNITY IS low;
        RULE 7 : IF ABILITY IS bad AND SOCIAL IS perfect THEN TRANSFER_OPPORTUNITY IS med;
        RULE 8 : IF ABILITY IS bad AND SOCIAL IS good THEN TRANSFER_OPPORTUNITY IS low;
        RULE 9 : IF ABILITY IS bad AND SOCIAL IS bad THEN TRANSFER_OPPORTUNITY IS low;
END_RULEBLOCK

END_FUNCTION_BLOCK


